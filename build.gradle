plugins {
    id 'java'
    id 'groovy'
    id 'antlr'
    id 'idea'
    id "com.palantir.graal" version "0.7.2"
}

group 'pw.avvero'
version '0.1.0'

sourceCompatibility = 11

repositories {
    mavenCentral()
}

dependencies {
    antlr "org.antlr:antlr4:4.7.1"
    implementation 'org.antlr:antlr4-runtime:4.7.1'
    implementation 'org.apache.commons:commons-lang3:3.9'
    implementation 'info.picocli:picocli:4.6.1'
    implementation "com.fasterxml.jackson.core:jackson-core:2.6.5"
    implementation "com.fasterxml.jackson.core:jackson-databind:2.6.5"

    compileOnly "org.projectlombok:lombok:1.18.12"
    annotationProcessor "org.projectlombok:lombok:1.18.12"
    annotationProcessor 'info.picocli:picocli-codegen:4.6.1'

    testCompileOnly "org.projectlombok:lombok:1.18.12"
    testAnnotationProcessor "org.projectlombok:lombok:1.18.12"

    testImplementation "org.codehaus.groovy:groovy-all:2.4.6"
    testImplementation "org.spockframework:spock-core:1.1-groovy-2.4-rc-2"
}

compileJava {
    options.compilerArgs += ["-Aproject=${project.group}/${project.name}"]
}

jar {
    manifest {
        attributes "Main-Class": "pw.avvero.jiac.App"
    }

    from {
        configurations.compileClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

generateGrammarSource {
    maxHeapSize = "64m"
    arguments += ['-package', 'pw.avvero.jiac.antlr']
    outputDirectory = new File("${project.buildDir}/generated-src/antlr/main/pw/avvero/jiac/antlr".toString())
}

idea {
    module {
        sourceDirs += file("${project.buildDir}/generated-src/antlr/main")
    }
}

graal.outputName("jiac")
graal.mainClass("pw.avvero.jiac.App")
graal.javaVersion("11")
graal.option("--enable-https")